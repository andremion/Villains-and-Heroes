apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.3"
    dataBinding {
        enabled = true
    }
    defaultConfig {
        applicationId "com.andremion.heroes"
        minSdkVersion 14
        targetSdkVersion 24
        versionCode 5
        versionName "1.2.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        abortOnError false
    }
}

def MARVEL_PRIVATE_KEY = System.getenv("MARVEL_PRIVATE_KEY") ?: marvelPrivateKey
def MARVEL_PUBLIC_KEY = System.getenv("MARVEL_PUBLIC_KEY") ?: marvelPublicKey

android.buildTypes.each { type ->
    type.buildConfigField "String", "MARVEL_PRIVATE_KEY", "\"$MARVEL_PRIVATE_KEY\""
    type.buildConfigField "String", "MARVEL_PUBLIC_KEY", "\"$MARVEL_PUBLIC_KEY\""
}

dependencies {
    compile "com.android.support:design:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    compile "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"
    compile "com.github.bumptech.glide:glide:$glideVersion"
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile "junit:junit:$junitVersion"
    androidTestCompile "com.android.support:support-annotations:$supportLibraryVersion"
    androidTestCompile 'com.android.support.test:rules:0.5'
    androidTestCompile("com.android.support.test.espresso:espresso-contrib:$espressoVersion") {
        exclude group: 'com.android.support'
    }
}
